from os.path import join as osjoin
import pandas as pd

# store the local path to the dropbox folder in the 'PROJ_HOME_DIR' file.
PROJ_HOME = open('PROJ_HOME_DIR').read().strip()
DATA_DIR = osjoin(PROJ_HOME, 'Data')
FIGURE_DIR = osjoin(PROJ_HOME, 'Figures')
RAW_DATA_DIR = osjoin(DATA_DIR, 'Raw')
ADDITIONAL_DATA_DIR = osjoin(DATA_DIR, 'Additional')
DERIVED_DATA_DIR = osjoin(DATA_DIR, 'Derived')
DESCRIPTIVE_DATA_DIR = osjoin(DATA_DIR, 'Descriptive')
SENTENCES_DATA_DIR = osjoin(DERIVED_DATA_DIR, 'Sentences')
EMBEDDINGS_DIR = osjoin(DERIVED_DATA_DIR, 'Embeddings')
UMAP_FIGURE_DIR = osjoin(FIGURE_DIR, 'UMAP')

############
# Raw data #
############
MOBILITY_RAW = osjoin(RAW_DATA_DIR, '20191024_mobility_transitions.txt')
MOBILITY_RAW_YEARLY = osjoin(RAW_DATA_DIR, 'yearly', '{year}_raw_mobility.csv')

######################
# Mobility sentences #
######################
MOBILITY_SENTENCES = osjoin(SENTENCES_DATA_DIR, '{year}_org_sentences.csv')

####################
# Descriptive Data #
####################
ORGANIZATION_FLOWS = osjoin(DESCRIPTIVE_DATA_DIR, 'org', 'organization_flows.csv')

##############
# Embeddings #
##############
WORD2VEC_EMBEDDINGS = osjoin(EMBEDDINGS_DIR, 'word2vec', '{period}_word2vec_d{dimensions}_model.bin')

##################
# Visualizations #
##################
UMAP_DATA = osjoin(UMAP_FIGURE_DIR, 'd{dimensions}', 'data', '{period}_umap_d{dimensions}_{metric}_neighbors{neighbors}_mindist{mindist}_data.csv')
UMAP_VISUALIZATIONS_ORG = osjoin(UMAP_FIGURE_DIR, 'd{dimensions}', '{period}_umap_d{dimensions}_{metric}_neighbors{neighbors}_mindist{mindist}_vis.{ext}')

##################
# Metadata files #
##################
ORG_SCALES = osjoin(ADDITIONAL_DATA_DIR, 'org_to_scales.txt')
ORG_LOOKUP = osjoin(ADDITIONAL_DATA_DIR, '20191024_institution_lookup.txt')
COUNTRY_LOOKUP = osjoin(ADDITIONAL_DATA_DIR, 'iso_to_country.txt')

##############
# Parameters #
##############
ALL_YEARS = range(2008, 2019, 1)
PERIODS = ['2008-2013', '2014-2019']
FIG_EXTS = ['.pdf', '.html']

# WORD2VEC parameters
W2V_DIMENSIONS = [50, 100, 150, 200]
W2V_WINDOW_SIZE = 20
W2V_MIN_WORD_FREQ = 50
W2V_NUM_WORKERS = 4
W2V_ITERATIONS = 10

# UMAP parameters
UMAP_METRICS = ['cosine', 'euclidean', 'correlation']
UMAP_NEIGHBORS = [5, 10, 15, 20]
UMAP_MIN_DIST = [0.0, 0.1, 0.25, 0.5]

def get_sentences_for_period(params):
    period_split = params.period.split("-")
    period_years = range(int(period_split[0]), int(period_split[1]) + 1, 1)
    return(expand(MOBILITY_SENTENCES, year = period_years))

###############################################################################
# TARGET RULE
###############################################################################
rule all:
    input:
        expand(WORD2VEC_EMBEDDINGS, period = PERIODS, dimensions = W2V_DIMENSIONS),
        ORGANIZATION_FLOWS,
        expand(UMAP_DATA,
               period = PERIODS,
               dimensions = W2V_DIMENSIONS,
               metric = UMAP_METRICS,
               neighbors = UMAP_NEIGHBORS,
               mindist = UMAP_MIN_DIST),
        expand(UMAP_VISUALIZATIONS_ORG,
               dimensions = W2V_DIMENSIONS,
               period = PERIODS,
               metric = UMAP_METRICS,
               neighbors = UMAP_NEIGHBORS,
               mindist = UMAP_MIN_DIST,
               ext = FIG_EXTS)

rule dissagregate_pubs_to_yearly:
    input: MOBILITY_RAW
    output: MOBILITY_RAW_YEARLY
    shell: "Rscript scripts/FilterRawMobilityToSingleYear.R {input} {wildcards.year} {output}"

rule pubs_to_sentences:
    input: rules.dissagregate_pubs_to_yearly.output
    output: MOBILITY_SENTENCES
    shell:
        "Rscript scripts/FormatPubsToSentences.R {input} org {output}"

rule train_word2vec_model:
    input: get_sentences_for_period
    output: WORD2VEC_EMBEDDINGS
    threads: W2V_NUM_WORKERS
    params:
        ws = W2V_WINDOW_SIZE,
        wf = W2V_MIN_WORD_FREQ,
        nw = W2V_NUM_WORKERS,
        niter = W2V_ITERATIONS
    shell:
        "python scripts/train_word2vec_embedding_from_sentences.py --files {input} \
                --dimensions {wildcards.dimensions} --window {params.ws} \
                --minfrequency {params.wf} --numworkers {params.nw} \
                --iterations {params.niter} --output {output}"

rule dimreduce_umap:
    input: rules.train_word2vec_model.output
    output: UMAP_DATA
    shell:
        'python scripts/dimreduce_with_umap.py --model {input} \
                --metric {wildcards.metric} --neighbors {wildcards.neighbors} \
                --mindistance {wildcards.mindist} --output {output}'

rule plot_umap_org:
    input: rules.dimreduce_umap.output
    params: ORG_LOOKUP, COUNTRY_LOOKUP
    output: UMAP_VISUALIZATIONS_ORG
    shell:
        "Rscript scripts/PlotOrgDimReducedEmbedding.R {input} {params} {output}"

rule calculate_org_flows:
    input: MOBILITY_RAW
    output: ORGANIZATION_FLOWS
    shell:
        "python scripts/calculate_org_flows.py --input {input} --output {output}"
